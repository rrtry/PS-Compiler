# Грамматика выражений языка ----

---
— статически типизированный язык программирования, сочетающий читаемость Python и структурность C-подобных языков.  
Данный документ описывает **грамматику выражений** в нотации **EBNF (ISO)**.

--- 

В языке существуют **два независимых класса выражений**:

1. **Числовые и логические выражения** — работают с `int` и `float`, участвуют в арифметике, сравнениях и условиях.
2. **Строковые выражения** — работают только с типом `string`, используются в присваиваниях, `print`, `concat`, `substr` и т.д.

Эти два класса **никогда не смешиваются** в одном выражении.

---

## 1. Числовые и логические выражения

Поддерживаются:
- Числовые литералы (`42`, `-10`, `3.14`, `0xFF`)
- Арифметические операторы (`+`, `-`, `*`, `/`, `%`, `^`, `**`)
- Унарные и постфиксные операторы (`+x`, `-x`, `!x`, `x++`, `x--`)
- Логические операторы (`&&`, `||`, `!`)
- Операторы сравнения (`==`, `!=`, `<`, `>`, `<=`, `>=`)
- Встроенные числовые функции (`abs(x)`, `pow(x,y)`, `min(...)`, `max(...)`)
- Скобки для группировки
- Константы `Pi`, `Euler`
- Логические литералы `true` (= 1), `false` (= 0)

### Приоритет операторов (от высшего к низшему)

| Приоритет | Операторы                              | Ассоциативность     |
|---------|----------------------------------------|---------------------|
| 1       | Постфиксные `++` `--`                  | —                   |
| 2       | `^` `**`                               | Правоассоциативная  |
| 3       | Унарные `+` `-` `!`, префиксные `++` `--` | —                |
| 4       | `*` `/` `%`                            | Левоассоциативная   |
| 5       | `+` `-`                                | Левоассоциативная   |
|  6      | `<` `>` `<=` `>=`                       | Левоассоциативная   |
|  7      | `==` `!=`                              | Левоассоциативная   |
|  8      | `&&`                                   | Левоассоциативная   |
|  9      | `||`                                   | Левоассоциативная   |
| 10      | Вызов функции, скобки `()`             | —                   |

### Встроенные числовые функции

| Функция         | Описание                              |
|-----------------|---------------------------------------|
| `abs(x)`        | Абсолютное значение                   |
| `pow(x, y)`     | Возведение в степень (`x ** y`)       |
| `min(a, b, …)`  | Минимальное значение                  |
| `max(a, b, …)`  | Максимальное значение                 |

---

## 2. Строковые выражения

Строковые выражения **не являются частью общего дерева числовых выражений**
и **не участвуют** в арифметических и логических операциях.

К строкам **запрещено** применять операторы:
`+ - * / % ^ && || ! < > <= >=`

Однако выражения сравнения вида:

    string_expression == string_expression
    string_expression != string_expression

**разрешены** и образуют **логическое выражение**
типа `int` со значением `1` (true) или `0` (false).

Допустимые строковые конструкции:
- Строковый литерал: `"Hello, world!"`
- Переменная типа `string`
- Вызовы: `concat(a, b)`, `substr(s, start, len)`
- Преобразование: `str(expr)` — числовое выражение → строка

Строковые выражения допустимы только в контекстах:
- инициализация и присваивание переменной типа `string`
- аргументы функций `print`, `concat`, `substr`
- аргументы пользовательских функций с параметрами типа `string`
- операнды операторов `==` и `!=`

---

## 3. Грамматика (EBNF ISO)

### 3.1. Числовые и логические выражения

```ebnf
numeric_expression = logical_or ;

logical_or     = logical_and , { "||" , logical_and } ;
logical_and    = equality , { "&&" , equality } ;

equality =
      relational , { ("==" | "!=") , relational }
    | string_expression , ( "==" | "!=" ) , string_expression ;

relational     = additive , { ("<" | ">" | "<=" | ">=") , additive } ;
additive       = multiplicative , { ("+" | "-") , multiplicative } ;
multiplicative = unary , { ("*" | "/" | "%") , unary } ;

unary          = { ("+" | "-" | "!" | "++" | "--") } , power ;
power          = postfix , { ("^" | "**") , power } ;
postfix        = primary , { "++" | "--" } ;

primary        = number
               | "Pi" | "Euler"
               | "true" | "false"
               | identifier
               | identifier , "(" , [ argument_list ] , ")"
               | "(" , numeric_expression , ")" ;
               
argument_list  = numeric_expression , { "," , numeric_expression } ;

number         = [ "-" ] , ( integer | float_literal ) ;
integer        = digit , { digit } | "0x" , hex_digit , { hex_digit } ;
float_literal  = integer , "." , integer ;
digit          = "0".."9" ;
hex_digit      = "0".."9" | "A".."F" | "a".."f" ;
identifier     = letter , { letter | digit | "_" } ;
letter         = "A".."Z" | "a".."z" ;

```

### 3.2. Строковые выражения

```ebnf
string_expression = string_literal
                  | identifier                                 (* переменная типа string *)
                  | "concat" , "(" , string_expression , "," , string_expression , ")"
                  | "substr" , "(" , string_expression , "," , numeric_expression , "," , numeric_expression , ")"
                  | "str" , "(" , numeric_expression , ")" ;

string_literal = """ , { character } , """ ;
character      = /* любой символ, кроме ", или \" */ ;
```